#version 330 core

// This will be edited on the fly by the shader compile code.
const int NUM_LIGHTS = XX;

// Input vertex data, different for all executions of this shader.
layout(location = 0) in vec3 vertexPosition_modelspace;
layout(location = 1) in vec2 vertexUV;
layout(location = 2) in vec3 vertexNormal_modelspace;

// Output data ; will be interpolated for each fragment.
out vec2 UV;
out vec3 Position_worldspace;
out vec3 Normal_cameraspace;
out vec3 EyeDirection_cameraspace;
out vec3 LightDirection_cameraspace[NUM_LIGHTS];
vec3 LightPosition_cameraspace[NUM_LIGHTS];

// Values that stay constant for the whole mesh.
uniform mat4 P;
uniform mat4 V;
uniform mat4 M;
uniform vec3 LightPosition_worldspace[NUM_LIGHTS];

void main(){

	// Output position of the vertex, in clip space : MVP * position
	gl_Position =  P * V * M * vec4(vertexPosition_modelspace,1);
	
	// Position of the vertex, in worldspace : M * position
	Position_worldspace = (M * vec4(vertexPosition_modelspace,1)).xyz;
	
	// Vector that goes from the vertex to the camera, in camera space.
	// In camera space, the camera is at the origin (0,0,0).
	vec3 vertexPosition_cameraspace =
    (V * M * vec4(vertexPosition_modelspace,1)).xyz;
	EyeDirection_cameraspace = vec3(0,0,0) - vertexPosition_cameraspace;

	// Vector that goes from the vertex to the light, in camera space. M
	// is not necessary because it's identity.
  for (int i = 0; i < NUM_LIGHTS; i++) { 
    LightPosition_cameraspace[i] = ( V * vec4(LightPosition_worldspace[i],1)).xyz;
    LightDirection_cameraspace[i] =
      LightPosition_cameraspace[i] + EyeDirection_cameraspace;
  }
	
	// Normal of the the vertex, in camera space
	Normal_cameraspace =
    (V * transpose(inverse(M)) * vec4(vertexNormal_modelspace,0)).xyz; 
	
	// UV of the vertex. No special space for this one.
	UV = vertexUV;
}

